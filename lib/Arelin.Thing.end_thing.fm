// Ends an animation/transformation/thing after some ticks
// - self: thing that will end after a while
// - tx: the new animation to use when it ends
// - after_tiks: numbers of ticks passed until the end
// - stt_key: the key in the stt map
Arelin.Thing.end_thing(
  self       : Arelin.Thing,
  tx         : Arelin.Game.TxFunction,
  after_tiks : F64,
  stt_key    : Bits 
  ) : Arelin.Thing
  let self.stt    = Arelin.Thing.get_stt(self)
  let current_tik = Map.get<>(stt_key, self.stt)
  let empty_stt   = Map.set<>(stt_key, after_tiks, self.stt)
  case current_tik:
  | #none# Arelin.Thing.set_stt(self, empty_stt);
  | #some#
    case F64.eql(current_tik.value, F64.0):
    | let self.pid = Arelin.Thing.get_pid(self)
      let self.pos = Arelin.Thing.get_pos(self)
      let self     = Arelin.Thing.set_fun(self, tx)
      let self     = Arelin.Thing.set_pid(self, self.pid)
      let self     = Arelin.Thing.set_pos(self, self.pos)
      let self     = Arelin.Thing.set_stt(self, empty_stt)
      self;  
    
    | let new_stt = 
        Map.set<>
        | stt_key;
        | case F64.gtn(current_tik.value, F64.0):
          | F64.sub(current_tik.value, F64.1);
          | F64.0;;
        | self.stt;
      Arelin.Thing.set_stt(self, new_stt);; 